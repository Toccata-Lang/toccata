
(inline C "#include <limits.h>\n")

;; There's a special hook in the compiler that will print the type of the input to this function
;; at compile time.
(defn show-type [x]
  x)

(defn is-type [x y]
  x)

(defn prefs [s v]
  ;; if you're using this function, you're so far off in the weeds, I can't help you
  ! s StringBuffer
  (inline C " prefs(extractStr(s_0), v_1);\nreturn(v_1);\n"))

;; TODO: definitely need a different 'def' for new types
;; what happens if try to redefine 'Sequence', for example
(def Sequence (any-of List
                      Vector))
(def String (any-of StringBuffer
                    SubString))
(def HashMap (any-of HashCollisionNode
                     ArrayNode
                     BitmapIndexedNode))

(defn maybe [v]
  !returns (maybe-of (type-of v))

  ;; wrap `v` in a Maybe value
  (inline C Maybe "return(maybe((FnArity *)0, (Value *)0, v_0));"))

(def int-max (inline C "(Value *)&(Integer){IntegerType, -2, INT_MAX};"))
(def int-min (inline C "(Value *)&(Integer){IntegerType, -2, INT_MIN};"))
(def empty-list (inline C "(Value *)&empty_list_struct"))
(def empty-vector (inline C "(Value *)&empty_vect_struct"))

(def nothing (inline C Maybe "(Value *)&(Maybe){MaybeType, -2, 0}"))

(defn fn-apply [f args]
  ! f Fn
  ! args List
  ;; TODO: make syntax to encode applying f to args
  ;; !returns (result-of f)
  !returns (apply-fn f args)
  (inline C "return(fnApply(f_0, args_1));"))

(defn default-type-name [value]
  ;; Return the string of the name of the type of `value`
  (inline C StringBuffer "
    Value *numVal = integerValue(value_0->type);
    dec_and_free(value_0, 1);
    return(integer_str(numVal));"))

(defn default-type-args [x]
  (inline C Vector "return(reifiedTypeArgs(x_0));"))

(defn get-refs [value]
  (inline C Integer "
    Value *numVal = integerValue(value_0->refs);
    dec_and_free(value_0, 1);
    return(numVal);"))

(defn default-get-type [value]
  (inline C Integer "
    Value *numVal = integerValue(value_0->type);
    dec_and_free(value_0, 1);
    return(numVal);"))

(defn mutate-vect-conj [v x]
  ! v Vector
  !returns (vector-of (any-of (type-of x)
                              (inner-type-of v)))

  (inline C Vector "return((Value *)mutateVectConj((Vector *)v_0, x_1));"))

(defn list-count [l]
  ! l List
  (inline C Integer "
   Value *numVal = integerValue(((List *)l_0)->len);
   dec_and_free(l_0, 1);
   return(numVal);"))

(defn list-map [l lf]
  ! lf (required-arities 1)
  ! l (list-of (type-of-arg lf 0))
  !returns (list-of (call-fn lf (inner-type-of l)))
  (inline C List "return(listMap(l_0, lf_1));"))

(defn list-concat [l]
  ! l (list-of Sequence)
  !returns (concat l)
  (inline C "return(listConcat(l_0));"))

(defn add-numbers [x y]
  ! x Integer
  ! y Integer
  (inline C Integer "
    Value *numVal = integerValue(((Integer *)x_0)->numVal + ((Integer *)y_1)->numVal);
    dec_and_free(x_0, 1);
    dec_and_free(y_1, 1);
    return(numVal);"))

(defn subtract-numbers [x y]
  ! x Integer
  ! y Integer
  (inline C Integer "
    Value *numVal = integerValue(((Integer *)x_0)->numVal - ((Integer *)y_1)->numVal);
    dec_and_free(x_0, 1);
    dec_and_free(y_1, 1);
    return(numVal);"))

(defn mult-numbers [x y]
  ! x Integer
  ! y Integer
  (inline C Integer "
    Value *numVal = integerValue(((Integer *)x_0)->numVal * ((Integer *)y_1)->numVal);
    dec_and_free(x_0, 1);
    dec_and_free(y_1, 1);
    return(numVal);"))

(defn div [x y]
  ! x Integer
  ! y Integer
  (inline C Integer "
    int denom = ((Integer *)y_1)->numVal;
    if (denom == 0) {
      fprintf(stderr, \"*** Can not divide by 0\\n\");
      abort();
    }
    Value *numVal = integerValue((int64_t)(((Integer *)x_0)->numVal / denom));
    dec_and_free(x_0, 1);
    dec_and_free(y_1, 1);
    return(numVal);"))

(defn mod [x y]
  ! x Integer
  ! y Integer
  (inline C Integer "
    int denom = ((Integer *)y_1)->numVal;
    if (denom == 0) {
      fprintf(stderr, \"*** Can not divide by 0\\n\");
      abort();
    }
    Value *numVal = integerValue(((Integer *)x_0)->numVal % denom);
    dec_and_free(x_0, 1);
    dec_and_free(y_1, 1);
    return(numVal);"))

(defn abort []
  (inline C Maybe
   "abort();
    return(nothing);"))

(defn vect-count [v]
  ! v Vector
  (inline C Integer "
   Value *result = integerValue(((Vector *)v_0)->count);
   dec_and_free(v_0, 1);
   return(result);"))

(def emptyBMI
  (inline C BitmapIndexedNode "(Value *)&emptyBMI"))

(defn bit-and [x y]
  ;; Bitwise AND two integer values
  ! x Integer
  ! y Integer
  (inline C Integer "return(bitAnd(x_0, y_1));"))

(defn bit-or [x y]
  ;; Bitwise OR two integer values
  ! x Integer
  ! y Integer
  (inline C Integer "return(bitOr(x_0, y_1));"))

(defn bit-xor [x y]
  ;; Bitwise XOR two integer values
  ! x Integer
  ! y Integer
  (inline C Integer "return(bitXor(x_0, y_1));"))

(defn bit-shift-left [x y]
  ;; Bitwise left shift of x by y bits
  ! x Integer
  ! y Integer
  (inline C Integer "return(bitShiftLeft(x_0, y_1));"))

(defn bit-shift-right [x y]
  ;; Bitwise right shift of x by y bits
  ! x Integer
  ! y Integer
  (inline C Integer "return(bitShiftRight(x_0, y_1));"))

(defn bit-not [x]
  ;; Bitwise NOT single integer value
  ! x Integer
  (inline C Integer "return(bitNot(x_0));"))

(defn deliver [p v]
  ;; Deliver value `v` to any thread waiting on promise `p`. Any thread that
  ;; calls `extract` on `p` after this will return with `v` immediately without waiting
  ! p Promise
  !returns (promise-of (type-of v))
  (inline C Promise "return(deliverPromise(p_0, v_1));"))

(defn delivered [p]
  ;; Test whether a value has been delivered for promise `p`
  ! p Promise
  !returns (maybe-of (type-of p))
  (inline C Maybe "return(promiseDelivered(p_0));"))

(defn cons [x l]
  ;; Add a value `x` to the head of list `l`
  ! l List
  !returns (list-as [(type-of x)] (type-of l))
  (inline C "
   Value *listVal = (Value *)listCons(x_0, (List *)l_1);
   return(listVal);\n"))

(defn sha1-init []
  (inline C Opaque "return(malloc_sha1());"))

(defn sha1-finalize [ctxt]
  ! ctxt Opaque
  (inline C Integer "return(finalize_sha1(ctxt_0));"))

(defn sha1-update-type [x ctxt]
  (inline C Integer "
  Sha1Update(((Opaque *)ctxt_1)->ptr, (void *)&((Integer *)x_0)->type, 8);
  dec_and_free(x_0, 1);
  return(ctxt_1);
"))

(defn address-of [x]
  (inline C Integer "return(integerValue((long long)x_0));"))

(defn identical [x y]
  ;; Returns a `maybe` value if and only if the `x` and `y` reside at the same address in memory
  !returns (maybe-of (type-of x))

  (inline C Maybe "
  if (x_0 == y_1) {
    dec_and_free(y_1, 1);
    return(maybe((FnArity *)0, (Value *)0, x_0));
  } else {
    dec_and_free(x_0, 1);
    dec_and_free(y_1, 1);
    return(nothing);
  }"))

(defn fn-name [f]
  ! f Fn
  (inline C StringBuffer "
  Value *strVal = stringValue(((Function *)f_0)->name);
  dec_and_free(f_0, 1);
  return(strVal);"))

(defn maybe-map [mv mf]
  ! mv (maybe-of (type-of-arg mf 0))
  ! mf (required-arities 1)
  !returns (maybe-of (call-fn mf (inner-type-of mv)))

  (inline C Maybe "return(maybeMap(mv_0, f_1));"))

(defn str-malloc [len]
  ! len Integer
  (inline C StringBuffer "
  String *strVal = malloc_string(((Integer *)len_0)->numVal);
  strVal->len = 0;
  strVal->buffer[0] = 0;
  dec_and_free(len_0, 1);
  return((Value *)strVal);\n"))

(defn str-append [dest src]
  ! dest StringBuffer
  ! src String
  (inline C StringBuffer "
  String *s_1 = (String *)dest_0;
  if (src_1->type == StringBufferType) {
    String *s2 = (String *)src_1;
    strncat(s_1->buffer, s2->buffer, s2->len);
    s_1->len += s2->len;
  } else if (src_1->type == SubStringType) {
    fprintf(stderr, \"appending to SubSting\\n\");
    abort();
    SubString *s2 = (SubString *)src_1;
    strncat(s_1->buffer, s2->buffer, s2->len);
    s_1->len += s2->len;
  }
  dec_and_free(src_1, 1);
  return(dest_0);"))

(defn pr* [str]
  ! str String
  (inline C Integer "return(prSTAR(str_0));"))

(defn pr-err* [str]
  ! str String
  (inline C Integer "return((*prErrSTAR)(str_0));"))

(defn escape-chars [s]
  ! s String
  (inline C StringBuffer "return(escapeChars(s_0));"))

(defn char [n]
  ;; Convert an integer to a one-character string
  ! n Integer
  (inline C StringBuffer "
  String *strVal = malloc_string(2);
  strVal->len = 1;
  strVal->buffer[0] = ((Integer *)n_0)->numVal;
  strVal->buffer[1] = 0;
  dec_and_free(n_0, 1);
  return((Value *)strVal);\n"))

(defn char-code [c]
  ;; Convert the first character of a string to an integer
  ! c String
  (inline C Integer "
   if (c_0->type == StringBufferType) {
     String *s = (String *)c_0;
     Value *numVal = integerValue((unsigned char)s->buffer[0]);
     dec_and_free(c_0, 1);
     return(numVal);
   } else if (c_0->type == SubStringType) {
     SubString *s = (SubString *)c_0;
     Value *numVal = integerValue((unsigned char)s->buffer[0]);
     dec_and_free(c_0, 1);
     return(numVal);
   }\n"))

(defn subs
  ;; Take a substring of a larger string
  ([src index]
   ! src String
   ! index (min-value 0)
   (inline C SubString "return(subs2(src_0, index_1));"))
  ([src index length]
   ! src String
   ! index (min-value 0)
   ! length (min-value 0)
   (inline C SubString "return(subs3(src_0, index_1, length_2));")))

(defn symbol [sym-str]
  ;; Convert a string to a symbol
  ! sym-str String
  (inline C Symbol "return(symbol(sym_str_0));"))

(defn add-promise-action [p f]
  ! p Promise
  ;; !returns (promise-of (result-of f))
  (inline C Promise "return(addPromiseAction((Promise *)p_0, f_1));"))

(defn promise
  ([] (inline C Promise "return((Value *)malloc_promise());\n"))
  ([v]
   ;; !returns (promise-of (type-of v))
   (deliver (promise) v)))

(defn future
  ([]
   (inline C Future "return(makeFuture((Value *)0));"))
  ([f]
   ! f (required-arities 0)
   (inline C Future "return(makeFuture(f_0));")))

(defn thread-id []
  (inline C Integer "return(integerValue((int64_t)pthread_self()));"))

(defn deliver-future [fut val]
  ! fut Future
  (inline C Future "return(deliverFuture(fut_0, val_1));"))

(defn add-future-action [fut f]
  ! fut Future
  (inline C Future "return(addFutureAction((Future *)fut_0, f_1));"))

(defn agent [v]
  (inline C Agent "return(makeAgent(v_0));"))

(defn integer-gen [x]
  (inline C Opaque "
  if (x_0->type != IntegerType) {
    fprintf(stderr, \"Invalid argument passed to 'integer-gen'\\n\");
    abort();
  }
  intGenerator *intGen = (intGenerator *)my_malloc(sizeof(intGenerator));
#ifdef CHECK_MEM_LEAK
  incTypeMalloc(14, 1);
#endif
  intGen->sym_counter = ((Integer *)x_0)->numVal;
  dec_and_free(x_0, 1);
  return((Value *)opaqueValue((void *)intGen, freeIntGenerator));
"))

(defn new-int [gen]
  ! gen Opaque
  (inline C Integer "
  intGenerator *gen = (intGenerator *)((Opaque *)gen_0)->ptr;
  dec_and_free(gen_0, 1);
  return(integerValue(gen->sym_counter++));
"))

(defn create-lazy []
  ;; private function used for implementing LazyList
  (inline C Opaque "
  extractCache *newCache = (extractCache *)my_malloc(sizeof(extractCache));
#ifdef CHECK_MEM_LEAK
  incTypeMalloc(14, 1);
#endif
  newCache->tail = malloc_list();
  pthread_mutex_init(&newCache->access, NULL);
  return((Value *)opaqueValue((void *)newCache, freeExtractCache));
"))

(defn get-lazy [lazy-struct]
  ! lazy-struct Opaque
  ;; private function used for implementing LazyList
  (inline C List "
  extractCache *cache = (extractCache *)((Opaque *)lazy_struct_0)->ptr;
  incRef((Value *)cache->tail, 1);
  dec_and_free(lazy_struct_0, 1);
  return((Value *)cache->tail);
"))

(defn first-lazy [lazy tail]
  ! lazy List
  ! tail Opaque
  ;; private function used for implementing LazyList
  (inline C Maybe "
  List *cache = (List *)lazy_0;
  extractCache *tail = (extractCache *)((Opaque *)tail_1)->ptr;
  if (cache->head == (Value *)0) {
    pthread_mutex_lock(&tail->access);
    if (cache->head == (Value *)0) {
      // leave the mutex locked and return nothing
      dec_and_free(lazy_0, 1);
      dec_and_free(tail_1, 1);
      return(nothing);
    } else {
// TODO: untested code path
fprintf(stderr, \"first-lazy 4\\n\");
abort();
      pthread_mutex_unlock(&tail->access);
      incRef(cache->head, 1);
      dec_and_free(lazy_0, 1);
      dec_and_free(tail_1, 1);
      return(maybe((FnArity *)0, (Value *)0, cache->head));
    }
  } else {
    Value *head = cache->head;
    incRef(head, 1);
    dec_and_free(lazy_0, 1);
    dec_and_free(tail_1, 1);
    return(maybe((FnArity *)0, (Value *)0, head));
  }
"))

(defn append-to-lazy-tail [tail value]
  ! tail Opaque
  ;; private function used for implementing LazyList
  (inline C Maybe "
  extractCache *cacheTail = (extractCache *)((Opaque *)tail_0)->ptr;
  cacheTail->tail->head = value_1;
  cacheTail->tail->tail = malloc_list();
  List *original = cacheTail->tail;
  cacheTail->tail = cacheTail->tail->tail;
  incRef((Value *)cacheTail->tail, 1);
  dec_and_free(tail_0, 1);
  dec_and_free((Value *)original, 1);
  pthread_mutex_unlock(&cacheTail->access);
  return(nothing);
"))

(defn rest-of-lazy [lazy]
  ! lazy List
  ;; private function used for implementing LazyList
  (inline C List "
  List *cache = (List *)lazy_0;
  Value *result;
  result = (Value *)cache->tail;
  incRef(result, 1);
  dec_and_free(lazy_0, 1);
  return(result);
"))

(defn null-term [s]
  ! s String
  (inline C StringBuffer "return((Value *)nullTerm(s_0));\n"))

;; protocol for implementing the hash map type
(defprotocol HashMapNode
  (hash-seq [m l]
    ! m HashMap
    ! l List
    ;; TODO: add key/val type to result type
    !returns List
    )

  (hash-vec [m v]
    ! m HashMap
    ! v Vector
    !returns Vector
    )

  (get* [m k v hash shift]
    ! m HashMap
    ! hash Integer
    ! shift Integer
    ;; TODO: add val type to result type
    !returns Maybe
    )

  (assoc* [m k v hash shift]
    ! m HashMap
    ! hash Integer
    ! shift Integer
    ;; TODO: add key/val type to result type
    !returns HashMap
    )

  (dissoc* [m k hash shift]
    ! m HashMap
    ;; TODO: if k is a literal, remove from return type
    ! hash Integer
    ! shift Integer
    !returns HashMap
    ))

;; Internal protocol for types.
(defprotocol Type
  (has-field [x field]
    ;; TODO: in generated code, this constraint doesn't appear, so there's a runtime check
    !returns (maybe-of (type-of x))

    ;; Does the type have `field` defined for it
    nothing)

  (get-type [value]
    !returns Integer
    ;; Return the Integer representing the type of `value`
    (default-get-type value))

  (type-name [value]
    ;; Return the string of the name of the type of `value`
    !returns StringBuffer
    (default-type-name value))

  (type-args [x]
    !returns Vector
    ;; Return the values for all the fields of `x` in a vector
    (default-type-args x))

  (instance? [t x]
    ;; test whether `x` is of type `t`
    ))

;; Definitions for the 'built-in' types
(def Integer
  (reify
    Type
    (instance? [_ x]
      (inline C (maybe-of Integer) "return(checkInstance(IntegerType, x_1));"))))

(def List
  (reify
    Type
    (instance? [_ x]
      (inline C (maybe-of List) "return(checkInstance(ListType, x_1));"))))

(def Maybe
  (reify
    Type
    (instance? [_ x]
      (inline C (maybe-of Maybe) "return(checkInstance(MaybeType, x_1));"))))

(def Symbol
  (reify
    Type
    (instance? [_ x]
      (inline C (maybe-of Symbol) "return(checkInstance(SymbolType, x_1));"))))

(def StringBuffer
  (reify
    Type
    (instance? [_ x]
      (inline C (maybe-of Symbol) "return(checkInstance(StringBufferType, x_1));"))))

(def SubString
  (reify
    Type
    (instance? [_ x]
      (inline C (maybe-of Symbol) "return(checkInstance(SubStringType, x_1));"))))

(def Vector
  (reify
    Type
    (instance? [_ x]
      (inline C (maybe-of Vector) "return(checkInstance(VectorType, x_1));"))))

(def Fn
  (reify
    Type
    (instance? [_ x]
      (inline C (maybe-of Fn) "return(checkInstance(FunctionType, x_1));"))))

(def Promise
  (reify
    Type
    (instance? [_ x]
      (inline C (maybe-of Promise) "return(checkInstance(PromiseType, x_1));"))))

(def Future
  (reify
    Type
    (instance? [_ x]
      (inline C (maybe-of Future) "return(checkInstance(FutureType, x_1));"))))

(def Agent
  (reify
    Type
    (instance? [_ x]
      (inline C (maybe-of Agent) "return(checkInstance(AgentType, x_1));"))))

;; The core protocols of Toccata
;; A type does not have to implement all the protocols or even all of the
;; protocol functions of a given protocol. In fact, most won't

;; For types whose values can be converted to human-readable strings
(defprotocol Stringable
  (string-list [x]
    ;; Create a list of strings of that comprise the representation of `x`
    !returns (list-of String)
    )

  (show* [x indent]
    ! indent String

    ;; Create a string list to show the value `x`
    !returns (list-of String)
    ))

;; For types whose values can be composed
(defprotocol Composition
  (zero [x]
    ;; Produce a value with 'same' type as `x` that can be `comp`d with any value
    ;;`y` of the same type to return `y`
    )

  (comp* [x xs]
    ! xs Sequence
    ;; Compose a number of values of the 'same' type to produce a single value of
    ;; that type. `xs` is a list of values of same type as `x`.
    ;; `comp*` must be associative
    ))

(defn comp
  ;; Compose values using the `comp*` protocol fn.
  ([x] x)
  ([x & xs]
   (comp* x xs)))

;; For types that contain values. Focus is on container
(defprotocol Container
  (flat-map [x f]
    ;; Apply `f` to the value(s) inside `x` and then flatten one level of nesting.
    ;; `f` must take one value and return a value of a type that's compatible with `x`
    ! f (required-arities 1)
    ! x (contains (type-of-arg f 0))
    !returns (result-of f)
    )

  (flatten [x]
    ;; Given a value `x` that contains value(s) of the same type as `x`, remove
    ;; one layer of wrapping.

    ;; the default way of flattening. Requires `flat-map` to be implemented
    ;; for `x`
    ;; (flat-map x identity)
    )

  (extend [x f]
    ;; Create a new value of same type as `x` that will apply `f` to
    ;; values `extract`ed from `x`.
    ;; (map (duplicate x) f)
    ! f (required-arities 1)
    )

  (duplicate [x]
    ;; Add a layer of wrapping to `x`
    ;; (extend x (fn [x] x))
    )

  (extract [x]
    ;; Pull a value out of `x`
    !returns (inner-type-of x)
    )

  (wrap [x v]
    ;; Create a new value of same type as `x` that contains the value `y`
    )

  (apply [xf xs]
    ;; Apply a function(s) in `xf` to the value(s) in each of the `xs`,
    ;; wrapping the result in the same type as `xf`.
    ! xs Sequence
    )

  (map [x f]
    ;; Create a new value of same type as `x` that contains the results of
    ;; applying `f` to all the values contained in `x`
    ! x (contains (type-of-arg f 0))
    ! f (required-arities 1)
    )

  (map [x f embed]
    ;; Create a new value embedded in a context of same type as `x` that contains the results of
    ;; applying `f` to all the values contained in `x`. The context the new value is embedded in
    ;; must implement 'flat-map'. If there are no values contained in 'x', the 'embed' function
    ;; is used to create an empty value of type 'x' embedded in the context. 
    ! x (contains (type-of-arg f 0))
    ! f (required-arities 1)
    ! embed (required-arities 1)
    ;; TODO: this is wrong. (result-of f) seems wrong
    !returns (any-of (result-of embed)
                     (result-of f))
    )

  (send* [x f-and-ys]
    ;; Cause the value(s) in `x` to be updated with the results of applying the
    ;; the first item of `f-and-ys` to each of the value(s) in `x` and the rest
    ;; of `f-and-ys`. Should only be implemented for types that support in place
    ;; updating.
    ! f-and-ys Sequence
    )

  ;; (assert (== (flat-map (wrap a p) g)
  ;;             (g p)))
  ;; (assert (== (flat-map (wrap a p) (fn [q]
  ;;                                    (wrap a q)))
  ;;             (wrap a p)))
  ;; (assert (== (flat-map (flat-map (wrap a p) g) h)
  ;;             (flat-map (wrap a p) (fn [q]
  ;;                                    (flat-map (g q) h)))))
  ;; (assert (== (apply (wrap p g) (list q))
  ;;             (map q g)))
  ;; (assert (== p (flatten (wrap p p))))
  ;; (assert (== (extend p extract) p))
  ;; (assert (== (extract (extend p g)) (g p)))
  ;; (assert (== (extend (extend p g) h)
  ;;             (extend p (fn [q]
  ;;                         (h (extend q g))))))
  ;; (assert (== (map (map p g) h)
  ;;             (map p (comp h g))))
  ;; (assert (== p (extract (duplicate p))))
  )

(defn send [v f & args]
  ;; send a function `f` to a value `v`. `f` will be applied to the contents of
  ;; `v` and the list of values in `args`
  (send* v (cons f args)))

;; TODO: putting this after it's use crashes the compiler
(def Predicate (all-of (required-arities 1)
                       (function-returns Maybe)))

;; For types that contain values. Focus is on the contained values.
;; No sense of ordering for contents required by this interface
(defprotocol Collection
  (empty? [coll]
    ;; Test whether `coll` contains any values
    !returns (maybe-of (type-of coll))
    )

  (empty [coll]
    ;; Create an empty collection of same type as `coll`
    !returns (type-of coll)
    )

  (count [coll]
    ;; Count the number of values in `coll`
    !returns Integer
    )

  (conj [coll x]
    ;; Add `x` to `coll`
    )

  (filter [coll f]
    ;; Create a new collection of same type as `coll` with only the values
    ;; for which `f` does not return `nothing`.

    ! f Predicate
    ! coll (contains (type-of-arg f 0))
    !returns (contains (type-of-arg f 0))
    )

  (reduce [coll x f]
    ! f (required-arities 2)
    ! coll (contains (type-of-arg f 1))
    !returns (any-of (type-of x)
                     (call-fn f (any-of (type-of x)
                                               (result-of f))
                                     (inner-type-of coll)))
    ))

;; Collection types that have some sense of ordering of their contents
(defprotocol Seqable
  (seq [coll]
    ;; Create a list of the contents
    !returns (list-from coll)
    )

  (vec [coll]
    ;; Create a vector of the contents
    !returns (vector-from coll)
    )

  (first [coll]
    ;; Return the first element in the `coll` (wrapped in a `Maybe`) if `coll`
    ;; is not empty. Returns 'nothing', if `coll` is empty.
    !returns (first-of coll)
    )

  (rest [coll]
    ;; Returns a new collection with all but the first value of `coll`
    !returns (rest-of coll)
    )

  (last [coll]
    ;; Return the last element in the `coll` (wrapped in a `Maybe`) if `coll`
    ;; is not empty. Returns 'nothing', if `coll` is empty.
    !returns (last-of coll)
    )

  (butlast [coll]
    ;; Returns a new collection with all but the last value of `coll`
    !returns (butlast-of coll)
    )

  (split [coll n]
    ;; divides a sequence into to parts with at most 'n' elements being in the first part
    ! n Integer
    !returns (vector-of [(contains (inner-type-of coll))
                         (contains (inner-type-of coll))])
    )

  (split-with [coll pred]
    ! pred Predicate
    !returns (vector-of [(contains (inner-type-of coll))
                         (contains (inner-type-of coll))])
    )

  (split-with [coll pred prefix]
    ! pred Predicate
    ! prefix Sequence
    !returns (vector-of [(contains (inner-type-of coll))
                         (contains (inner-type-of coll))])
    )

  (take [coll n]
    ! n Integer
    !returns (contains (inner-type-of coll))
    )

  (drop [coll n]
    ! n Integer
    !returns (contains (inner-type-of coll))
    )

  (drop-while [coll pred]
    ! pred Predicate
    !returns (contains (inner-type-of coll))
    )

  (take-while [coll pred]
    ! pred Predicate
    !returns (contains (inner-type-of coll))
    )

  (reverse [coll]
    ;; Create a new collection of same type as `coll` with the contents
    ;; in reverse order
    )

  (to-str [coll]
    ;; Builds a string from the string representation of all the values in `coll`
    !returns StringBuffer
    ))

(defn second [coll]
  ;; get the second element from the sequable `coll`
  (first (rest coll)))

;; For collections whose contents can be indexed by integers
(defprotocol Indexed
  (nth [coll n]
    ;; Retrieve the `n`th value from `coll`, wrapped in a Maybe, if there are
    ;; enough values in `coll`. Otherwise, returns `nothing`.
    ! n Integer
    !returns (maybe-of (inner-type-of coll))
    )

  (store [coll n v]
    ;; Create a new copy of `coll` (wrapped in a Maybe)  with `v` at index `n`
    ;; if `coll` is at least size of `n` - 1. Otherwise, return `nothing`.
    ! n Integer
    !returns (maybe-of (any-of (inner-type-of coll)
                               (type-of v)))
    ))

;; For types whose values can be hashed to an integer
(defprotocol Hashable
  (sha1-update [x context]
    ! context Opaque
    )

  (sha1 [x]
    ;; Compute the SHA1 hash of `x`
    !returns Integer
    ))

;; For types that emulate a key/value store
(defprotocol Associative
  (assoc [m k v]
    ;; TODO: update key and value types
    )

  (get [m k]
    ;; Retrieve the value associated with `k` in `m`, wrapped in a `Maybe` if it exists.
    ;; Otherwise, return `nothing`
    ;; TODO: add value type to result
    !returns Maybe
    )

  (keys [m]
    ;; Get a list of the keys from `m`
    ;; TODO: add key type to result
    !returns List
    )

  (vals [m]
    ;; Get a list of the vals from `m`
    ;; TODO: add value type to result
    !returns List
    ))

;; For types whose values may be invoked like functions
(defprotocol Function
  (invoke [_])
  (invoke [_ _])
  (invoke [_ _ _])
  (invoke [_ _ _ _])
  (invoke [_ _ _ _ _])
  (invoke [_ _ _ _ _ _])
  (invoke [_ _ _ _ _ _ _])
  (invoke [_ _ _ _ _ _ _ _])
  (invoke [_ _ _ _ _ _ _ _ _]))

; For types that have some notion of equality between their values
(defprotocol Eq
  (=* [x y]
    ;; TODO: should be
    ;; !returns (maybe-of (all-of (type-of x)
    ;;                            (type-of y)))
    ;; Compare two values for equality
    !returns (maybe-of (type-of x))
    ))

;; For types that have some notion of order of their values
(defprotocol Ord
  (<* [x y]
    ;; Compare two values for order
    !returns (maybe-of (type-of x))
    ))

;; interface to low-level functions
(extend-type List
  Eq
  (=* [x y]
    (inline C "return(listEQ(x_0, y_1));"))

  Collection
  (filter [coll f]
    (inline C List "return(listFilter(coll_0, f_1));"))
  
  Sequable
  (reverse [l]
    !returns (contains (inner-type-of l))
    (inline C List "return((Value *)reverseList((List *)l_0));"))

  (first [l]
    (inline C "return(car(l_0));"))

  (rest [l]
    (inline C "return(cdr(l_0));"))

  (vec [l]
    !returns (vector-from l)
    (inline C "return((Value *)listVec(l_0));")))

(extend-type Vector
  Seqable
  (reverse [v]
    !returns (contains (inner-type-of v))
    (inline C Vector "return(vectorReverse(v_0));"))

  (seq [v]
    !returns (list-from v)
    (inline C "return(vectSeq((Vector *)v_0, 0));\n"))

  Indexed
  (store [v n x]
    (inline C "
  Value *result = vectStore((Vector *)v_0, ((Integer *)n_1)->numVal, (Value *)x_2);
  dec_and_free(v_0, 1);
  dec_and_free(n_1, 1);
  return(result);"))

  Associative
  (get [v n]
    ! n (all-of Integer
                (min-value 0))
    !returns (maybe-of (inner-type-of v))
    (inline C Maybe "return(vectorGet(v_0, n_1));"))

  Collection
  (conj [vect v]
    !returns (vector-of (any-of (inner-type-of vect)
                                (type-of v)))
    (inline C Vector "
  Value *result = (Value *)vectConj((Vector *)vect_0, v_1);
  dec_and_free(vect_0, 1);
  return(result);")))

(defn identity [x]
  x)

;; This is the declaration of the 'list' function. Calls to 'list' are inlined by the compiler
(defn list [& l]
  ;; construct a list of values from the arguments
  l)

;; This is the declaration of the 'vector' function. Calls to 'vector' are inlined by the compiler
(defn vector [& l]
  ;; construct a vector of values from the arguments
  (vec l))

(defn partial [f & args]
  ;; paritally apply `f` to `args` creating a new function that expects more arguments
  (fn [& more-args]
    (apply f (list-concat (list args more-args)))))

;; TODO: implement Float as well.
;; pay attention to https://blog.acolyer.org/2020/09/28/fpspy/
(extend-type Integer
  Type
  (type-args [n]
    [n])

  (type-name [_]
    "Integer")

  Stringable
  (string-list [n]
    (inline C "return((Value *)listCons(integer_str(n_0), empty_list));\n"))

  (show* [x indent]
    (string-list x))

  Eq
  (=* [x y]
    (inline C "return(integer_EQ(x_0, y_1));"))

  Ord
  (<* [x y]
    (inline C "return(integerLT(x_0, y_1));"))

  Hashable
  (sha1-update [x ctxt]
    (inline C Opaque "
  Sha1Update(((Opaque *)ctxt_1)->ptr, (void *)&((Integer *)x_0)->type, 8);
  Sha1Update(((Opaque *)ctxt_1)->ptr, (void *)&((Integer *)x_0)->numVal, 8);
  dec_and_free(x_0, 1);
  return(ctxt_1);
"))

  (sha1 [x]
    (inline C Integer "return(integerValue(integerSha1(x_0)));")))

(defn inc [x]
  ;; Add 1 to an integer
  (add-numbers 1 x))

(defn dec [x]
  ;; Subtract 1 from an integer
  ;; ! Integer x

  (subtract-numbers x 1))

(defn maybe-apply [mv m-args args]
  ! mv Maybe
  ! m-args (sequence-of Maybe)
  ! args Vector
  !returns (maybe-of (result-of (inner-type-of mv)))

  ;; TODO: rewrite using maybe-map
  (either (maybe-map (first m-args)
                     (fn [m-arg]
                       (either (map m-arg (fn [arg]
                                            (maybe-apply mv (rest m-args) (conj args arg))))
                               nothing)))
          (maybe-map mv (fn [f]
                          (apply f args)))))

(extend-type Maybe
  Type
  (type-args [v]
    (either (maybe-map v vector)
            empty-vector))

  (type-name [_] "Maybe")

  Hashable
  (sha1-update [x ctxt]
    (sha1-update-type x ctxt)
    (either (maybe-map x (fn [v]
                           (sha1-update v ctxt)))
            ctxt))

  (sha1 [x]
    (let [ctxt (sha1-init)]
      (sha1-update x ctxt)
      (sha1-finalize ctxt)))

  Eq
  (=* [x y]
    (inline C Maybe "return(maybeEQ(x_0, y_1));"))

  Stringable
  (string-list [mv]
    (either (maybe-map mv (fn [v]
                            (comp (list "<maybe ")
                                  (string-list v)
                                  (list ">"))))
            (list "<nothing>")))

  (show* [x indent]
    (either (maybe-map x (fn [v]
                           (comp (list "(maybe ")
                                 (show* v (comp indent "       "))
                                 (list ")"))))
            (list "nothing")))

  Container
  (extract [mv]
    !returns (inner-type-of mv)

    (inline C "return(maybeExtract(mv_0));"))

  (map [mv f]
    (maybe-map mv f))

  (map [mv f embed]
    (either (maybe-map mv (fn [v]
                            (map (f v) maybe)))
            (embed nothing)))

  (wrap [_ v]
    (maybe v))

  (apply [mv args]
    (maybe-apply mv (seq args) []))

  Composition
  (zero [mv]
    nothing)

  Associative
  (get [m _]
    m)

  (assoc [x y v]
    (maybe v)))

(extend-type Maybe
  Container
  (flat-map [mv f]
    ! mv (maybe-of (type-of-arg f 0))
    ! f Predicate
    !returns (call-fn f (inner-type-of mv))

    (and mv (f (extract mv)))))

(defn list-reduce [l result f]
  ! l (list-of (type-of-arg f 1))
  ! f (required-arities 2)
  !returns (any-of (type-of result)
                   (call-fn f (any-of (type-of result)
                                             (result-of f))
                                   (inner-type-of l)))

  (cond (=* 0 (list-count l))
        result

        (list-reduce (rest l) (f result (extract (first l))) f)))

(defn apply-to-vectors [f vects arg-vects]
  (either (for [vect (first vects)]
            (apply-to-vectors f (rest vects)
                              (reduce vect [] (fn [new-args v]
                                                (reduce arg-vects new-args
                                                        (fn [new-args arg-vect]
                                                          (conj new-args (conj arg-vect v))))))))
          (map arg-vects (fn [args]
                           (apply f (seq args))))))

(defn chop-list [l n prefix]
    ! prefix List
    !returns (vector-of List)

    (either (map (and (<* 0 n)
                      (first l))
                 (fn [head]
                   (chop-list (rest l) (dec n) (cons head prefix))))
            [prefix l]))

(extend-type List
  Type
  (type-name [_]
    "List")

  (type-args [l]
    (vec l))

  Hashable
  (sha1-update [l ctxt]
    (sha1-update-type l ctxt)
    (list-reduce l ctxt (fn [ctxt x]
                          (sha1-update x ctxt))))

  (sha1 [l]
    (let [ctxt (sha1-init)]
      (sha1-update l ctxt)
      (sha1-finalize ctxt)))

  Composition
  (zero [_]
    empty-list)

  (comp* [l ls]
    (list-concat (cons l (list-map (seq ls) seq))))

  Collection
  (count [l]
    (list-count l))

  (empty? [l]
    (and (=* 0 (list-count l))
         (maybe empty-list)))

  (empty [_]
    empty-list)

  (conj [l v]
    (cons v l))

  (reduce [l result f]
    (list-reduce l result f))

  Container
  (map [l f]
    (list-map l f))

  (map [l f embed]
    (-> l
        reverse
        (list-reduce (embed empty-list)
                     (fn [v x]
                       (flat-map (f x)
                                 (fn [y]
                                   (map v (fn [result-list]
                                            (cons y result-list)))))))))

  (wrap [x v]
    (list v))

  (flatten [ls]
    (list-concat ls))

  (flat-map [l mf]
    ! l (list-of (type-of-arg mf 0))
    ! mf (all-of (required-arities 1)
                 (function-returns List))
    !returns (call-fn mf (inner-type-of l))

    (list-concat (list-map l mf)))

  (apply [mf lists]
    !returns (list-of (result-of (inner-type-of mf)))

    (either (for [f (first mf)
                  first-list (first lists)]
              (seq (apply-to-vectors f
                                     (rest lists)
                                     (map first-list vector))))
            empty-list))
 
  Seqable
  (to-str [coll]
    (let [ss-list (list-concat (list-map coll string-list))
          new-len (list-reduce ss-list 0 (fn [len s]
                                           (add-numbers len (count s))))]
      (list-reduce ss-list (str-malloc new-len) str-append)))

  (split-with [l pred prefix]
    ! prefix List
    !returns (vector-of [List List])

    (either (for [head (first l)
                  :when (pred head)]
              (split-with (rest l) pred (cons head prefix)))
            [(reverse prefix) l]))

  (seq [l]
    l)

  (butlast [coll]
    (let [[before _] (chop-list coll (dec (list-count coll)) empty-list)]
      (reverse before)))

  (split [l n]
    (let [[before after] (chop-list l n empty-list)]
      [(reverse before) after]))

  Indexed
  ;; TODO: rewrite using chop-list
  (nth [coll n]
    (or (and (=* n 0) (first coll))
        (and (<* 0 n) (nth (rest coll) (dec n))))))

(extend-type String
  Collection
  (count [s]
    (inline C Integer "return(strCount(s_0));")))

(extend-type String
  Seqable
  (first [s]
    (map (<* 0 (count s))
         (fn [_]
           (subs s 0 1))))

  (seq [coll]
    !returns (list-from coll)
    (inline C "return(strSeq(coll_0));"))

  (vec [coll]
    !returns (vector-from coll)
    (inline C "return(strVec(coll_0));")))

(extend-type String
  Type
  (type-name [_]
    "String")

  (type-args [s]
    [s])

  Stringable
  (string-list [s]
    (list s))

  (show* [s indent]
    (list "\"" s "\""))

  Composition
  (zero [_]
    "")

  Collection
  (empty? [coll]
    (and (=* 0 (count coll))
         (maybe "")))

  (empty [coll]
    "")

  (reduce [s x f]
    (inline C "return(strReduce(s_0, x_1, f_2));"))

  Seqable
  (last [s]
    (let [n (count s)]
      (map (<* 0 n)
           (fn [_]
             (subs s (dec n))))))

  (butlast [s]
    (subs s 0 (dec (count s))))

  (reverse [s]
    !returns StringBuffer
    (to-str (reverse (seq s))))

  (rest [s]
    (subs s 1))

  (split-with [s pred prefix]
    ! prefix Vector
    ;; TODO: this should cause a runtime error
    ;; because of that call to 'to-str' a few lines down
    !returns (vector-of [SubString SubString])

    ;; TODO: rewrite this to not call split-with recursively
    (either (for [head (first s)
                  :when (pred head)]
              (split-with (subs s 1) pred (conj prefix head)))
            [(to-str prefix) s]))

  (split [s n]
    [(subs s 0 n) (subs s n)])

  (take [s n]
    (subs s 0 n))

  (drop [s n]
    (subs s n))

  Indexed
  (nth [s n]
    (map (<* n (count s))
         (fn [_]
           (subs s n 1))))

  Eq
  (=* [x y]
    (inline C "return(strEQ(x_0, y_1));"))

  Ord
  (<* [x y]
    (inline C "return(strLT(x_0, y_1));"))

  Hashable
  (sha1-update [x ctxt]
    (inline C Opaque "
  strSha1Update(((Opaque *)ctxt_1)->ptr, x_0);
  dec_and_free(x_0, 1);
  return(ctxt_1);
"))

  (sha1 [s]
    (inline C Integer "return(integerValue(strSha1(s_0)));")))

(extend-type StringBuffer
  Seqable
  (to-str [s]
    s))

(extend-type SubString
  Seqable
  (to-str [s]
    (str-append (str-malloc (count s)) s)))

(extend-type String
  Seqable
  (split-with [s pred]
    (split-with s pred [])))

(extend-type String
  Seqable
  (drop-while [coll pred]
    (let [[_ tail] (split-with coll pred)]
      tail))

  (take-while [coll pred]
    (let [[prefix _] (split-with coll pred)]
      prefix)))

(defn list*
  ([] empty-list)

  ([arg]
   ! arg Sequence

   (seq arg))

  ([arg & args]
   !returns List

   (let [[arg-list & args] (reverse (cons arg args))]
     ! arg-list Sequence

     (list-reduce args (seq arg-list) conj))))

(defn interpose [coll sep]
  ! coll Sequence
  !returns (sequence-of (any-of (inner-type-of coll)
                                (type-of sep)))

  ;; build list by inserting `sep` between each of the elements of `coll`
  (rest (flat-map coll (partial list sep))))

(extend-type List
  Seqable
  (split-with [l pred]
    (split-with (rest l) pred empty-list)))

(extend-type List
  Stringable
  (string-list [l]
    (list "(" (to-str (list-concat (list-map (interpose l ", ") string-list))) ")"))

  (show* [x indent]
    (either (map (empty? x) string-list)
            (let [[h & tail] x]
              (comp (cons "(" (show* h (comp indent " ")))
                    (flat-map tail (fn [x]
                                     (list* ",\n" indent " "
                                            (show* x (comp indent " ")))))
                    (list ")")))))

  Seqable
  (last [coll]
    ;; TODO: remove use of 'nth'
    (nth coll (dec (count coll))))

  (take [coll n]
    (let [[prefix _] (split coll n)]
      prefix))

  (drop [coll n]
    (let [[_ tail] (split coll n)]
      tail))

  (drop-while [coll pred]
    (let [[_ tail] (split-with coll pred)]
      tail))

  (take-while [coll pred]
    (let [[prefix _] (split-with coll pred)]
      prefix))

  Associative
  (get [l k]
    ;; TODO: show-type this
    (nth l k)))

(defn str [& vs]
  ;; Converts the list of arguments into a single string of characters. Every argument
  ;; must implment the `string-list` protocol function.
  !returns String

  (to-str vs))

